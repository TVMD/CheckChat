apply plugin: 'com.android.application'
apply plugin: 'android-apt'
apply plugin: 'com.neenbedankt.android-apt'
def AAVersion = '4.1.0'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.3"

    defaultConfig {
        applicationId "com.app.checkchat"
        minSdkVersion 15
        renderscriptTargetApi 21
        renderscriptSupportModeEnabled true
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"
        multiDexEnabled true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    dexOptions {
        javaMaxHeapSize "4g"
    }
    productFlavors {
        dev {

        }

        staging {

        }

        production {

        }
    }
}

apt {
    arguments {
        androidManifestFile variant.outputs[0]?.processResources?.manifestFile
        resourcePackageName 'com.app.checkchat'
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    //Core
    apt "org.androidannotations:androidannotations:$AAVersion"
    apt 'com.google.dagger:dagger-compiler:2.0'
    provided 'javax.annotation:jsr250-api:1.0'

    //Utils
    compile group: 'com.squareup.okhttp3', name: 'logging-interceptor', version: '3.3.1'


    

    compile 'com.beesightsoft:mobile-core:0.1.3'
    compile 'com.android.support:recyclerview-v7:23.4.0'
    compile 'com.android.support:support-v4:23.4.0'
    compile 'com.facebook.android:facebook-android-sdk:4.+'
    compile 'com.joanzapata.android:base-adapter-helper:1.1.11'
    compile 'de.hdodenhof:circleimageview:2.1.0'
    compile 'com.fivehundredpx:blurringview:1.0.0'
    compile 'com.mobsandgeeks:android-saripaar:2.0.3'
    compile 'com.github.bumptech.glide:glide:3.7.0'
    compile 'com.android.support:design:23.4.0'
    compile 'com.google.android.gms:play-services-location:8.4.0'
    compile 'com.google.android.gms:play-services:8.4.0'
    compile 'jp.wasabeef:glide-transformations:2.0.1'
    compile 'com.kennyc:snackbar:2.5'

}

apply plugin: 'checkstyle'
apply plugin: 'findbugs'
apply plugin: 'pmd'

checkstyle {
    configFile file("${project.rootDir}/config/quality/checkstyle/checkstyle.xml")
    toolVersion = '6.16.1'
}

task checkstyle(type: Checkstyle) {
    configProperties.checkstyleSuppressionsPath = file("${project.rootDir}/config/quality/checkstyle/suppressions.xml").absolutePath
    source 'src'
    include '**/*.java'
    exclude '**/gen/**'
    classpath = files()
}

task findbugs(type: FindBugs) {
    ignoreFailures = false
    effort = "max"
    reportLevel = "high"
    excludeFilter = new File("${project.rootDir}/config/quality/findbugs/findbugs-filter.xml")
    classes = files("${project.rootDir}/app/build/intermediates/classes")

    source 'src'
    include '**/*.java'
    exclude '**/gen/**'

    reports {
        xml.enabled = false
        html.enabled = true
        xml {
            destination "$project.buildDir/reports/findbugs/findbugs.xml"
        }
        html {
            destination "$project.buildDir/reports/findbugs/findbugs.html"
        }
    }

    classpath = files()
}

task pmd(type: Pmd) {
    ignoreFailures = false
    ruleSetFiles = files("${project.rootDir}/config/quality/pmd/pmd-ruleset.xml")
    ruleSets = []

    source 'src'
    include '**/*.java'
    exclude '**/gen/**'

    reports {
        xml.enabled = false
        html.enabled = true
        xml {
            destination "$project.buildDir/reports/pmd/pmd.xml"
        }
        html {
            destination "$project.buildDir/reports/pmd/pmd.html"
        }
    }
}

check.dependsOn 'checkstyle', 'findbugs', 'pmd'
